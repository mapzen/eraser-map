buildscript {
  ext.kotlin_version = '1.0.0-beta-1103'
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
  }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'checkstyle'

def VECTOR_TILE_API_KEY = hasProperty('vectorTileApiKey') ? '"' + vectorTileApiKey + '"' : "null";
def PELIAS_API_KEY = hasProperty('peliasApiKey') ? '"' + peliasApiKey + '"' : "null";
def VALHALLA_API_KEY = hasProperty('valhallaApiKey') ? '"' + valhallaApiKey + '"' : "null";
def MINT_API_KEY = hasProperty('mintApiKey') ? '"' + mintApiKey + '"' : "null";
def BUILD_NUMBER = hasProperty('buildNumber') ? '"' + buildNumber + '"' : '"DEV"';

def RELEASE_STORE_FILE = hasProperty('releaseStoreFile') ? releaseStoreFile : "null";
def RELEASE_STORE_PASSWORD = hasProperty('releaseStorePassword') ? releaseStorePassword : "null";
def RELEASE_KEY_ALIAS = hasProperty('releaseKeyAlias') ? releaseKeyAlias : "null";
def RELEASE_KEY_PASSWORD = hasProperty('releaseStoreFile') ? releaseKeyPassword : "null";

android {
  compileSdkVersion 22
  buildToolsVersion "23.0.1"

  defaultConfig {
    applicationId "com.mapzen.erasermap"
    minSdkVersion 15
    targetSdkVersion 22
    versionCode 10
    versionName "0.7-SNAPSHOT"
    buildConfigField "String", "VECTOR_TILE_API_KEY", VECTOR_TILE_API_KEY
    buildConfigField "String", "PELIAS_API_KEY", PELIAS_API_KEY
    buildConfigField "String", "VALHALLA_API_KEY", VALHALLA_API_KEY
    buildConfigField "String", "MINT_API_KEY", MINT_API_KEY
    buildConfigField "String", "BUILD_NUMBER", BUILD_NUMBER
  }

  signingConfigs {
    debug {
      storeFile file("debug.keystore")
    }

    release {
      storeFile file(RELEASE_STORE_FILE)
      storePassword RELEASE_STORE_PASSWORD
      keyAlias RELEASE_KEY_ALIAS
      keyPassword RELEASE_KEY_PASSWORD
    }
  }

  buildTypes {
    debug {
      signingConfig signingConfigs.debug
    }
    release {
      signingConfig signingConfigs.release
      minifyEnabled false
    }

    productFlavors {
      dev {
      }
      prod {
      }
    }
  }

  sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
    test.java.srcDirs += 'src/test/kotlin'
  }

  packagingOptions {
    exclude 'META-INF/LICENSE.txt'
  }

  testOptions {
    unitTests.returnDefaultValues = true
  }
}

tasks.withType(Test) {
  testLogging {
    exceptionFormat "full"
    events "started", "skipped", "passed", "failed"
    showStandardStreams true
  }
}

task checkstyle(type: Checkstyle) {
  configFile file("${project.rootDir}/config/checkstyle/checkstyle.xml")
  source 'src'
  include '**/*.java'
  include '**/*.kt'
  exclude '**/gen/**'

  classpath = files()
}

repositories {
  mavenLocal()
  mavenCentral()
  maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
  maven { url "https://mint.splunk.com/gradle/" }
}

ext {
  flavorType = "Dev"
}

gradle.startParameter.getTaskNames().each { task ->
  if(task.contains("Prod")){
    flavorType = "Prod"
  }
}

dependencies {
  compile 'com.android.support:appcompat-v7:22.2.0'
  compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
  compile 'com.mapzen.tangram:tangram:0.1-SNAPSHOT'
  compile 'com.mapzen.android:lost:1.0.2-SNAPSHOT'
  compile('com.mapzen.android:pelias-android-sdk:0.7-SNAPSHOT') {
    exclude group: 'javax.annotation:javax', module: 'javax.annotation-api'
  }
  compile 'com.google.dagger:dagger:2.0'
  compile 'com.google.dagger:dagger-compiler:2.0'
  compile 'javax.annotation:javax.annotation-api:1.2'
  compile 'com.android.support:support-v4:22.2.0'
  compile 'com.squareup:otto:1.3.7'
  compile('com.mapzen:on-the-road:0.8-SNAPSHOT') {
    exclude group: 'org.apache.commons', module: 'commons-io'
  }
  compile 'com.splunk.mint:mint:4.2.1'
  compile 'com.squareup.okhttp:okhttp:2.4.0'
  compile 'com.mapzen.android:speakerbox:1.2.0'

  testCompile 'junit:junit:4.12'
  testCompile 'org.mockito:mockito-core:1.9.5'
  testCompile 'org.assertj:assertj-core:1.7.0'
  testCompile('org.robolectric:robolectric:3.0') {
    exclude group: 'commons-logging', module: 'commons-logging'
    exclude group: 'org.apache.httpcomponents', module: 'httpclient'
  }
  testCompile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

  if (flavorType == 'Prod') {
    println("Including production only dependencies...")
    compile 'com.mapzen.android:leyndo:0.0.1-SNAPSHOT'
  }
}
